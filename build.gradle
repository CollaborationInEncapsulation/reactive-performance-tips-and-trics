import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
  id 'org.springframework.boot' version '2.3.1.RELEASE' apply false
  id 'io.spring.dependency-management' version '1.0.9.RELEASE' apply false
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'io.spring.dependency-management'

  group = 'org.coinen.reactive.store'
  version = '0.0.1-SNAPSHOT'
  sourceCompatibility = '11'

  repositories {
	mavenCentral()
	mavenLocal()
	maven { url 'https://repo.spring.io/release' }
	maven { url 'https://repo.spring.io/milestone' }
  }

  ext {
	set('testcontainersVersion', '1.14.3')
	set('springCloudVersion', "Hoxton.SR5")
  }

  configurations {
	compileOnly {
	  extendsFrom annotationProcessor
	}
  }

  dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'

	implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'
	implementation 'org.springframework.cloud:spring-cloud-starter-sleuth'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-rsocket'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'io.projectreactor.addons:reactor-extra'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	runtimeOnly 'io.r2dbc:r2dbc-postgresql'
	runtimeOnly 'org.postgresql:postgresql'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
	  exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'org.testcontainers:testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:r2dbc'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'io.projectreactor:reactor-test'


	implementation 'net.bytebuddy:byte-buddy-agent:1.10.9'
	implementation 'net.bytebuddy:byte-buddy:1.10.9'
	implementation 'io.projectreactor:reactor-debug-agent:2.0.0-SNAPSHOT'
  }

  dependencyManagement {
	imports {
	  mavenBom(SpringBootPlugin.BOM_COORDINATES)
	  mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	  mavenBom("org.testcontainers:testcontainers-bom:${testcontainersVersion}")
	}
  }

  test {
	useJUnitPlatform()
  }
}